title: Tutorial1
tags: 
colorID: 0
position: 611,517
---
<<ChangeSpeaker SoundManager Boss>>
Finally! Took you long enough! All the goblins are getting massacred, and necromancy costs are through the roof! 
You have to fix this! You're a wizard, right?! Where did you study?

[[I was Magus Alumni at Albion U.|Tutorial12]]
[[School is for losers.|Tutorial11]]

<<if $timeMagicSpecific is true>>
[[At Albion U, I did my graduate work in time magic.|Tutorial122]]
<<endif>>
===
title: Tutorial122
tags: 
colorID: 0
position: 1124,836
---
Really now? So what number am I thinking of?

<<if $guessProof is true>>
[[24,601.|Tutorial1222]]
<<endif>>

[[Uh.. 42?|Tutorial1221]]
[[It is 7 for sure.|Tutorial1221]]
[[Let me guess, 410?|Tutorial1221]]
[[1066, definitely.|Tutorial1221]]

===
title: Tutorial121
tags: 
colorID: 0
position: 1344,485
---
So you can you cast fireballs or something, right?

[[W-well... not actually.|Tutorial1211]]
===
title: Tutorial111
tags: 
colorID: 1
position: 1081,-44
---
<<ChangeSpeaker SoundManager Boss>>
What?! Then we're done here, find some other fool to hire you!

<<ChangeSpeaker SoundManager Voiceless>>
[Perhaps I shouldn't be so flippant about my education next time.]

<<if $hasTimeTraveled is false>>
<<TogglePrompts UIManager 4>>
<<set $hasTimeTraveled to true>>
<<endif>>


===
title: Tutorial12
tags: 
colorID: 0
position: 1002,522
---
Albion U? That place is renowned for it's wizardry! What was your speciality?

[[I didn't really focus on one area, more like a general knowledge of wizardy.|Tutorial121]]

<<if $timeMagicSpecific is true>>
[[I did my dissertation on the morals and ethics of time magic.|Tutorial122]]
<<endif>>
===
title: Tutorial1211
tags: 
colorID: 0
position: 1740,425
---

<<set $timeMagicSpecific to true>>

<<if $bossTimeMagicNote is false>>
<<RevealText Boss Maybe_I_should_mention_my_doctoral_work_on_time_magic.>>
<<set $bossTimeMagicNote to true>>

<<endif>>

<<if $hasJournaled is false>>
<<TogglePrompts UIManager 3>>
<<set $hasJournaled to true>>
<<endif>>

...
[[Tutorial12111]]
===
title: Tutorial1221
tags: 
colorID: 1
position: 1742,671
---
<<set $guessProof to true>>
<<if $bossGuessNote is false>>
<<RevealText Boss Now_to_prove_him_wrong._The_number_is_24601!>>
<<set $bossGuessNote to true>>
<<endif>>

Not even close! It was 24,601. 
I was looking for a skilled wizard! Get out of here!

<<ChangeSpeaker SoundManager Voiceless>>
[Now to prove him wrong!]

===
title: Tutorial1222
tags: 
colorID: 1
position: 1742,962
---
That... how did you? Was that luck?!
...Nevermind, those heroes are likely on their way back soon, I'll show you to the goblins!
You have one meeting to make this work, or I'm going to get my money back out of your hide.
<<EndTutorial Boss>>
===
title: Tutorial12111
tags: 
colorID: 1
position: 1745,132
---
<<ChangeSpeaker SoundManager Boss>>
Then what's the point?! Come back when you learn something useful!

<<ChangeSpeaker SoundManager Voiceless>>
[Maybe I should mention my doctoral work on time magic.]

<<if $hasTimeTraveled is false>>
<<TogglePrompts UIManager 4>>
<<set $hasTimeTraveled to true>>
<<endif>>
===
title: Tutorial11
tags: 
colorID: 0
position: 836,225
---
<<if $bossFlippantNote is false>>
<<RevealText Boss Perhaps_I_shouldn't_be_so_flippant_about_my_education_next_time.>>
<<set $bossFlippantNote to true>>
<<endif>>

<<if $hasJournaled is false>>
<<TogglePrompts UIManager 3>>
<<set $hasJournaled to true>>
<<endif>>

...
[[Tutorial111]]

===
